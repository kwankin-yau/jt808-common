plugins {
    id 'java-library'
    id 'scala'
    id 'maven-publish'
    id 'signing'
}

group 'com.lucendar'
version '3.1.0-SNAPSHOT'

compileJava.options.encoding = 'UTF-8'
compileScala.options.encoding = 'UTF-8'

sourceCompatibility = '8'
targetCompatibility = '8'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api('org.scala-lang:scala-library:2.13.10')
    api group: 'com.typesafe.scala-logging', name: 'scala-logging_2.13', version: '3.9.2'

    api('io.netty:netty-all:4.1.104.Final')

    implementation 'org.checkerframework:checker-qual:3.36.0'
    implementation('commons-codec:commons-codec:1.15')
    implementation('org.apache.commons:commons-lang3:3.11')
    api('com.google.code.gson:gson:2.10.1')
    implementation('commons-validator:commons-validator:1.7')
    implementation('dnsjava:dnsjava:3.4.2')

    implementation('com.lucendar:lucendar-common:2.1.0-SNAPSHOT')
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.encoding = 'UTF-8'

    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java


            pom {
                name = 'jt808-common'
                packaging = 'jar'
                // optionally artifactId can be defined here
                description = 'JT/T 808、1078、ADAS 协议编解码库'
                url = 'https://github.com/kwankin-yau/jt808-common'

                scm {
                    connection = 'https://github.com/kwankin-yau/jt808-common.git'
                    developerConnection = 'https://github.com/kwankin-yau/jt808-common.git'
                    url = 'https://github.com/kwankin-yau/jt808-common'
                }

                licenses {
                    license {
                        name = 'MIT'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'kky'
                        name = 'kwankin-yau'
                        email = 'alphax@vip.163.com'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}


